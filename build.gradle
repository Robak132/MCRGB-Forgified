plugins {
    id 'idea'
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '6.0.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
    id 'org.spongepowered.mixin' version '0.7.+'
}

def secretsFile = rootProject.file(".secret.properties")
ext.secrets = new Properties()
if (secretsFile.exists()) {
    secretsFile.withInputStream { stream ->
        secrets.load(stream)
    }
}

def getSecret(String key, fallback = null) {
    return secrets.getProperty(key) ?: System.getenv(key) ?: fallback
}

repositories {
    maven { url "https://maven.shedaniel.me/" }
    maven { url "https://maven.minecraftforge.net" }
    maven { url 'https://jitpack.io' }
    maven {
        url = uri("https://maven.pkg.github.com/Robak132/LibGui-Forgified")
        credentials {
            username = getSecret("GITHUB_ACTOR")
            password = getSecret("GITHUB_TOKEN")
        }
    }
    mavenCentral()
}

version = project.mod_version
group = project.maven_group
base.archivesName = project.archives_base_name

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    toolchain { languageVersion = JavaLanguageVersion.of(17) }
    withSourcesJar()
}

minecraft {
    mappings channel: 'parchment', version: '2023.09.03-1.20.1'
    copyIdeResources = true
    generateRunFolders = true
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        configureEach {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        gameTestServer {
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            workingDirectory project.file('run-data')

            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft("net.minecraftforge:forge:1.20.1-47.4.0")
    if (getSecret("DEV", true)) {
        implementation project(":LibGui-Forge")
    } else {
        compileOnly fg.deobf("io.github.robak132:libgui_forge:8.1.3")
    }
    api fg.deobf("me.shedaniel.cloth:cloth-config-forge:11.1.136")

    compileOnly("org.projectlombok:lombok:1.18.42")
    annotationProcessor("org.projectlombok:lombok:1.18.42")

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

// Proper Mixin setup for Forge
mixin {
    add sourceSets.main, 'mixins.mcrgb_forge.refmap.json'
    config 'mixins.mcrgb_forge.json'

    debug.verbose = true
    debug.export = true
}